" VIMRC

syntax enable

" I WILL BE V-IMPROVED
set nocompatible

set redrawtime=10000

" required
filetype plugin indent on

" make sure vim plug is installed
if empty(glob('~/.vim/autoload/plug.vim'))
  silent !curl -fLo ~/.vim/autoload/plug.vim --create-dirs
    \ https://raw.githubusercontent.com/junegunn/vim-plug/master/plug.vim

  augroup SETUP
    autocmd!
    autocmd VimEnter * PlugInstall --sync | source $MYVIMRC
  augroup END
endif

" I use space as my leader key
let g:mapleader="\<Space>"

function! s:SourceConfigFilesIn(directory)
  let directory_splat = '$HOME/dotfiles/vim/' . a:directory . '/*'

  for config_file in split(glob(directory_splat), '\n')
    if filereadable(config_file)
      execute 'source' config_file
    endif
  endfor
endfunction

function! s:MakeVimDir(directory)
  let s:vim_cache = expand('~/.vim/' . a:directory)

  if filewritable(s:vim_cache) == 0 && exists('*mkdir')
    call mkdir(s:vim_cache, 'p')
  endif
endfunction

call plug#begin('~/.vim/bundle')
call s:SourceConfigFilesIn('rcplugins')
call plug#end()
call s:SourceConfigFilesIn('rcfiles')

" backup and store all swapfiles in one location
call s:MakeVimDir('backup')
call s:MakeVimDir('swp')
set backupdir=~/.vim/backup//
set directory=~/.vim/swp//
set tags=./tags,tags;$HOME

" consistent background theme everywhere
if filereadable(expand('~/.vimrc_background'))
  let base16colorspace=256
  source ~/.vimrc_background
endif
